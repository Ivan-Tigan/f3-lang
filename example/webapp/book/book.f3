system include "./f3auth.f3", "./time.f3", "./events-table.f3".
f3Auth encryptionSecretKey "Some secret key";
    clientId "0xdrjgnfvjdpn7wfgs1z2";
    clientSecret "CDlhWVPjoX0K6yZe9BDuajrRgEsbZ3DI";
    loginPath "/f3auth/login";
    logoutPath "/f3auth/logout";
    callbackPath "/f3auth/callback";
    appDomain "localhost";
    loginRedirect "/";
    logoutRedirect "/".
(
    f3Auth logoutPath ?LogoutPath.
    ?R [f3AuthSession >> match] (name = ?UserName.).
    system cut [].
    [ "<div class='f3-auth-widget'> <span>Welcome, " ?UserName "!</span> <a href='" ?LogoutPath "'>Logout</a> </div>" ] sconcat ?HTML.
) => (?R f3AuthWidget ?HTML.).
(
    f3Auth loginPath ?LoginPath.
    [ "<div class='f3-auth-widget' > <span>Not logged in</span> <a href='" ?LoginPath "'>Login</a> </div>" ] sconcat ?HTML.
) => (?R f3AuthWidget ?HTML.).

( ?R match (?RID headers (host = ?Host.).). ?Host [[splitString "."] >> [0]] ?RSub. ?RSub neq "bbookk".) 
=> ( ?R subdomain ?RSub.).

(?XS sconcat ?S. ["<html data-theme='light'> <head> <title>F3 Auth Demo</title> 
"
// <link rel='stylesheet' href='https://cdn.jsdelivr.net/npm/@picocss/pico@2/css/pico.min.css'>

// <link rel='stylesheet' href='https://outstanding-winter.surge.sh/picogram.css'>
// <link rel='stylesheet' href='http://127.0.0.1:5500/picogram.css'>
"
<link rel=\"stylesheet\" href=\"https://npmcdn.com/select-combobox/dist/combobox.css\">


<link rel='stylesheet' href='https://fonts.googleapis.com/css?family=Roboto:300,300italic,700,700italic'>
<meta name='viewport' content='width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no'>

 <link rel='stylesheet' href='https://cdnjs.cloudflare.com/ajax/libs/milligram/1.4.1/milligram.css'>
 <link rel='stylesheet' href='https://cdnjs.cloudflare.com/ajax/libs/normalize/8.0.1/normalize.css'>
<style>
nav { display: flex; justify-content: space-between; align-items: center; padding: 1rem 0; }
nav ul { display: flex; list-style: none; margin: 0; padding: 0; align-items: center; }
nav ul li { margin-right: 1rem; display: flex; margin: auto; }
fieldset[role='group'] { display: flex; border: 1px solid #d1d5db; border-radius: 0.375rem; padding: 0; margin: 0; }
fieldset[role='group'] input { border: none; border-radius: 0; margin: 0; flex: 1; }
fieldset[role='group'] input[type='submit'] { flex: none; width: min-content; }
fieldset[role='group'] input:not(:last-child) { border-right: 1px solid #d1d5db; }
fieldset[role='group'] input:first-child { border-top-left-radius: 0.375rem; border-bottom-left-radius: 0.375rem; }
fieldset[role='group'] input:last-child { border-top-right-radius: 0.375rem; border-bottom-right-radius: 0.375rem; }

p.breadcrumbs { margin: 0.5rem 0; color: #666; font-size: 0.9em; }
p.breadcrumbs span { color: #333; font-weight: 500; }
p.breadcrumbs a:not(:first-child)::before, p.breadcrumbs span:not(:first-child)::before { content: '>'; margin-right: 0.4rem; margin-left: 0.5rem; }

</style>


 </head> <body><main class='container'>" ?S "</main></body></html>" ] sconcat ?H. ) 
=> (?XS buildHTML ?H.).

(?R f3AuthWidget ?F3AuthWidget. ["<nav> <ul><li>bbookk.io</li></ul> <ul> <li>" ?F3AuthWidget "</li> </ul></nav>"] sconcat ?NavHTML. ) 
=> (?R navbar ?NavHTML.).


(
   ?R f3AuthSession ?F3AS. ?F3AS match ( sub = ?UserId. name = ?UserName. email = ?UserEmail. ).
   ?txn lmdbStartRW "./db"; lmdbInsert { ?UserId a User; name ?UserName; email ?UserEmail.}; lmdbEnd [].
) => ( ?R initTenant ?F3AS.).

( 
    ?r [f3AuthSession >> match ] ( sub = ?UserId. ). 
    ?txn lmdbStartR "./db"; lmdbQuery { ?UserId a User; tenantSubdomain ?s. }; lmdbEnd []. 
    system cut [].
) 
=> ( ?r tenantSubdomainString ?s.).
?r tenantSubdomainString "".

(?r match (?RID port 80; protocol "http".). system cut [].) 
=> (?r portString "".).
(?r match (?RID port 443; protocol "https".). system cut [].) 
=> (?r portString "".).
(?r match (?RID port ?Port.). [":" ?Port] sconcat ?PortString.) 
=> (?r portString ?PortString.).

(
    f3Auth appDomain ?AppDomain.
   ?r match ( ?RID protocol ?Protocol. ); [f3AuthSession >> match ] ( sub = ?UserId. ); tenantSubdomainString ?S; portString ?PortString.
    system cut [].
    [?Protocol "://" ?S "." ?AppDomain ?PortString] sconcat ?AppURL.
   // 2@html
   ["<form action='/set-tenant-subdomain' method='post'>
   <fieldset role='group'>
   <input type='text' name='subdomain' required value='" ?S "'>
   <input type='submit' value='Set Subdomain'>
   </fieldset>
   <small>Your app is available at <a href='" ?AppURL "'> " ?AppURL "</a></small>
   </form>
   " ] sconcat ?HTML.
) => ( ?r tenantSubdomainForm ?HTML. ).
?R tenantSubdomainForm "".

( 
   ?r match ( ?RID a post; path "/set-tenant-subdomain"; body ( subdomain = ?Subdomain. ); headers ( referer = ?referer.).);
    [f3AuthSession >> match] ( sub = ?UserId. ). 
   ?txn lmdbStartRW "./db"; lmdbDelete {?UserId tenantSubdomain ?OldSubdomain.}; lmdbInsert { ?UserId tenantSubdomain ?Subdomain. }; lmdbEnd [].
) => ( ?r response (?RID status 302; headers ( "Location" = ?referer. ). ).  ).

(
     ?r [f3AuthSession >> match] ( sub = ?UserId. ).
    ?Txn lmdbStartR "./db". 
    [?Option ( 
        ?Txn lmdbQuery { ?UserId calendar ?CalendarId. ?CalendarId name ?CalendarName.}. 
        ["<tr><td>" ?CalendarName "</td><td><a href='/calendar/" ?CalendarId "'>Edit</a></td></tr>"] sconcat ?Option. )
    ] collect ?Options.
    ?Txn lmdbEnd [].
    system cut [].
    ?Options sconcat ?OptionsHTML.
    ["<table>
    <thead>
        <tr>
            <th>Calendar Name</th>
            <th>Action</th>
        </tr>
    </thead>
    <tbody>
    " ?OptionsHTML "
    </tbody>
    </table>"] sconcat ?SelectHTML.
) => (?r calendarSelect ?SelectHTML.).
?r calendarSelect "<table><thead><tr><th>Calendar Name</th><th>Action</th></tr></thead><tbody></tbody></table>".


?R calendarForm "<form action='/create-calendar' method='post'>
<fieldset role='group'>
    <input type='text' placeholder='Calendar Name' name='calendarname' required>
    <input type='submit' value='Create Calendar'>
 </fieldset>   
    </form>".

( 
   ?r match ( ?RID a post; path "/create-calendar"; body ( calendarname = ?CalendarName. ); headers ( referer = ?referer.).);
    [f3AuthSession >> match] ( sub = ?UserId. ). 
   ?RID sha256 ?RIDHash.
   ["calendar_" ?RIDHash] sconcat ?CalendarId.
   ?txn lmdbStartRW "./db"; lmdbInsert { ?UserId calendar ?CalendarId. ?CalendarId a Calendar; name ?CalendarName. }; lmdbEnd [].
) => ( ?r response (?RID status 302; headers ( "Location" = ?referer. ). ).  ).

(
    ?R match ( ?RID a get; path "/".); navbar ?Nav; [initTenant >> toString]  ?F3AuthSessionInfo; 
        tenantSubdomainForm ?tenantSubdomainForm;
        calendarForm ?CalendarForm;
        calendarSelect ?CalendarSelect.
    system cut [].
    // 1@html
    [
        ?Nav 
        "<h1>Dashboard</h1>"
        ?tenantSubdomainForm
        ?CalendarForm
        ?CalendarSelect
        "<div>Session Info: <span>" ?F3AuthSessionInfo "</span></div>"
    ] buildHTML ?ResponseHTML.
) => ( ?R response (
    ?RID status 200; 
    headers ( "Content-Type" = "text/html; charset=utf-8". );
    body ?ResponseHTML.
). ).

(
    ?R match ( ?RID a get; path "/".); navbar ?Nav; subdomain ?Tenant. 
    ?Txn lmdbStartR "./db"; lmdbQuery { ?User a User; name ?UserName; tenantSubdomain ?Tenant. }; lmdbEnd [].
    system cut [].

    [?Nav "<h1>Welcome the calendar of " ?UserName "</h1>"] buildHTML ?ResponseHTML. 
)
=> (?R response (?RID status 200; headers ( "Content-Type" = "text/html; charset=utf-8". ); body ?ResponseHTML. ).).

(?R match ( ?RID a get; path "/".); navbar ?Nav. [?Nav "<h1>Welcome to bbookk.io</h1>"] buildHTML ?ResponseHTML. )
=> (?R response (?RID status 200; headers ( "Content-Type" = "text/html; charset=utf-8". ); body ?ResponseHTML. ).).

(
    ?R match ( ?RID a get; path ?Path.); navbar ?Nav; 
    [f3AuthSession >> match] ( sub = ?UserId. ).
    ?Path [splitString "/"] ?PathSplit.
    ?PathSplit = ["" "calendar" ?CalendarId].
    ?Txn lmdbStartR "./db"; lmdbQuery { ?UserId calendar ?CalendarId. ?CalendarId name ?CalendarName. }; lmdbEnd [].
    system cut [].
    ["<a href='/calendar/" ?CalendarId "/create-event'><button>Create Event</button></a>"] sconcat ?CreateEventButton.


    ?R eventsTable ?EventsTable.
    
    [?Nav "
    <p class='breadcrumbs'><a href='/'>Home</a><span>" ?CalendarName "</span></p>
    <h1>" ?CalendarName "</h1> " ?CreateEventButton ?EventsTable] buildHTML ?ResponseHTML.
) => (?R response (?RID status 200; headers ( "Content-Type" = "text/html; charset=utf-8". ); body ?ResponseHTML. ).).


// create event feature
(
    ?R match ( 
        ?RID a get
        ; path ?Path.
    )
    ; navbar ?Nav
    ; [f3AuthSession >> match] ( 
        sub = ?UserId. 
    ).
    
    ?Path [splitString "/"] ["" "calendar" ?CalendarId "create-event"].

    ?Txn lmdbStartR "./db"
    ; lmdbQuery { 
        ?UserId calendar ?CalendarId. 
        ?CalendarId name ?CalendarName. 
    }
    ; lmdbEnd [].
    
    system cut [].
    
    ["<h1>Create Event for " ?CalendarName "</h1>
    <form action='/calendar/" ?CalendarId "/create-event' method='post'>
        <label for='eventname'>Event Name</label>
        <input type='text' id='eventname' placeholder='Event Name' name='eventname' required>
        <label for='description'>Description</label>
        <input type='text' id='description' placeholder='Description' name='description' required>
        <label for='start'>Start Date & Time</label>
        <input type='datetime-local' id='start' name='start' required>
        <label for='duration'>Duration</label>
        <input type='time' id='duration' name='duration' required>
        <label for='frequency'>Frequency</label>
        <select id='frequency' name='frequency' required>
            <option value='single'>Single</option>
            <option value='weekly'>Weekly</option>
        </select>
        <label for='capacity'>Capacity</label>
        <input type='number' id='capacity' placeholder='Capacity' name='capacity' min='1' required>
        <label for='paymentmethods'>Payment Methods (comma separated)</label>
        <input type='text' id='paymentmethods' placeholder='Payment Methods (comma separated)' name='paymentmethods'>
        <label for='finaliteration'>Final Iteration</label>
        <input type='number' id='finaliteration' placeholder='Final Iteration' name='finaliteration' min='1'>
        <label for='openbookings'>Open Bookings Before Start</label>
        <input type='time' id='openbookings' placeholder='Open Bookings Before Start' name='openbookings'>
        <input type='submit' value='Create Event'>
    </form>"] sconcat ?CreateEventForm.
    
    [?Nav ?CreateEventForm] buildHTML ?ResponseHTML.
) => (?R response (?RID status 200; headers ( "Content-Type" = "text/html; charset=utf-8". ); body ?ResponseHTML. ).).

(
    ?R match ( 
        ?RID a post
        ; path ?Path
        ; body ( 
            eventname = ?EventName. 
            description = ?Description. 
            start = ?Start. 
            duration = ?Duration. 
            frequency = ?Frequency. 
            capacity = ?Capacity.
            paymentmethods = ?PaymentMethods. 
            finaliteration = ?FinalIteration.
            openbookings = ?OpenBookings. 
        )
        ; headers ( 
            referer = ?referer.
        ).
    )
    ; [f3AuthSession >> match] ( sub = ?UserId. ).
    
    ?Path [splitString "/"]  ["" "calendar" ?CalendarId "create-event"].
    
    ?RID sha256 ?RIDHash.
    ["event_" ?RIDHash] sconcat ?EventId.
    
    ?Start parseTimeStamp ?StartTimeStamp.
    ?Duration parseTime ?DurationTime.
    ?OpenBookings parseTime ?OpenBookingsTime.
    
    ?txn lmdbStartRW "./db"
    ; lmdbInsert { 
        ?CalendarId event ?EventId. 
        ?EventId a Event
        ; name ?EventName
        ; description ?Description
        ; start ?StartTimeStamp
        ; duration ?DurationTime
        ; frequency ?Frequency
        ; capacity ?Capacity
        ; paymentMethods ?PaymentMethods
        ; finalIteration ?FinalIteration
        ; openBookingsXSecondsBeforeStart ?OpenBookingsTime. 
    }
    ; lmdbEnd [].
    
    ["/calendar/" ?CalendarId] sconcat ?RedirectPath.
) => ( ?R response (?RID status 302; headers ( "Location" = ?RedirectPath. ). ).  ).



// Bookings display page
(
    ?R match ( 
        ?RID a get
        ; path ?Path.
    ); navbar ?Nav.
    ?Path [splitString "/"] ["" "event" ?EventId ?IterationS "bookings"].
    ?Txn lmdbStartR "./db"
    ; lmdbQuery { 
        ?CalendarId name ?CalendarName; event ?EventId. 
        ?EventId name ?EventName
        ; description ?Description
        ; start ?StartTF
        ; duration ?DurationT
        ; capacity ?Capacity. 
    }.

    ?StartTF truncate ?StartT.
    ?IterationS [parseInteger >> truncate] ?Iteration.
    time secondsPerWeek ?SecondsPerWeek.
    clpfd constraint [
        ?StartT + ?Iteration * ?SecondsPerWeek = ?EventIterationStartT
    ].
    clpfd label [?EventIterationStartT].
    ?EventIterationStartT [timeStampToString "%a, %d %b %Y %H:%M"] ?EventIterationStartStr.
    ?EventIterationStartT [+ ?DurationT] ?EventIterationEndT.
    ?EventIterationEndT [timeStampToString "%H:%M"] ?EventIterationEndStr.
    
    [?BookingRow (
        ?Txn lmdbQuery { 
            ?BookingId a Booking
            ; event ?EventId
            ; iteration ?Iteration
            ; name ?BookingName
            ; email ?BookingEmail
            ; phone ?BookingPhone.
        }.
        ( 
            ?Txn lmdbQuery {
                ?BookingId timeStamp ?BookingTimeStamp. 
            }.
            ?BookingTimeStamp [timeStampToString "%d %b %Y %H:%M"]  ?BookingTimeStampStr.
        ) or ( 
            ?BookingTimeStampStr = "---".
        ).

        (
            ?Txn lmdbQuery {
                ?BookingId cancelled ?CTime. 
            }. 
            ?CTime [timeStampToString "%d %b %Y %H:%M"] ?CancelledTime.
            ?CancelRow = "---".
        ) or (
            ?CancelledTime = "---".
            ["<a href='/bookings/" ?BookingId "/cancel'>Cancel</a>"] sconcat ?CancelRow.
        ).

        ["<tr>
            <td>" ?BookingName "</td>
            <td>" ?BookingEmail "</td>
            <td>" ?BookingPhone "</td>
            <td>" ?BookingTimeStampStr "</td>
            <td>" ?CancelledTime "</td>
            <td>" ?CancelRow "</td>
        </tr>"] sconcat ?BookingRow.
    )] [collect >> sconcat] ?BookingRows.
    
    ?Txn lmdbEnd [].
    
    ["
    <p class='breadcrumbs'><a href='/'>Home</a><a href='/calendar/" ?CalendarId "'>" ?CalendarName "</a><span>" ?EventName "</span></p>

    <h1>Bookings for " ?EventName "</h1>
        <p><strong>Description:</strong> " ?Description "</p>
    <p><strong>Date & Time:</strong> " ?EventIterationStartStr " - " ?EventIterationEndStr "</p>
    <p><strong>Capacity:</strong> " ?Capacity "</p>
    
    <table>
        <thead>
            <tr>
                <th>Name</th>
                <th>Email</th>
                <th>Phone</th>
                <th>Booking Time</th>
                <th>Cancelled</th>
                <th>Actions</th>
            </tr>
        </thead>
        <tbody>
            " ?BookingRows "
        </tbody>
    </table>"] sconcat ?BookingsContent.
    // ?BookingsContent = "Test".
    [?Nav ?BookingsContent] buildHTML ?ResponseHTML.
) => (
    ?R response (
        ?RID status 200
        ; headers ( 
            "Content-Type" = "text/html; charset=utf-8". 
        ); body ?ResponseHTML. 
    ).
).

// Booking cancellation GET - shows auto-submit form
(
    ?R match ( 
        ?RID a get
        ; path ?Path
        ; headers ( 
            referer = ?RefererURL.
        ).
    ); navbar ?Nav.
    ?Path [splitString "/"] ["" "bookings" ?BookingId "cancel"].
    
    ["<h1>Cancelling Booking...</h1>
    <p>Please wait while we process your cancellation.</p>
    
    <form id='cancelForm' action='/bookings/" ?BookingId "/cancel' method='post' style='display: none;'>
        <input type='hidden' name='referer' value='" ?RefererURL "'>
    </form>
    
    <script>
        window.onload = function() {
            document.getElementById('cancelForm').submit();
        };
    </script>"] sconcat ?CancelContent.
    
    [?Nav ?CancelContent] buildHTML ?ResponseHTML.
) => (
    ?R response (
        ?RID status 200
        ; headers ( 
            "Content-Type" = "text/html; charset=utf-8". 
        ); body ?ResponseHTML. 
    ).
).

// Booking cancellation POST - actual cancellation logic
(
    ?R match ( 
        ?RID a post
        ; path ?Path
        ; body ( 
            referer = ?RefererURL.
        ).
    ); navbar ?Nav.
    ?Path [splitString "/"] ["" "bookings" ?BookingId "cancel"].
    system log [DDDDDDDDDDDd 1].
    ?Txn lmdbStartRW "./db".
    ?Txn lmdbQuery { 
        ?BookingId a Booking
        ; event ?EventId
        ; iteration ?Iteration
        ; name ?BookingName
        ; email ?BookingEmail
        ; phone ?BookingPhone.
    }.
    
    system log [DDDDDDDDDDDd 2].
    system [now >> truncate] ?CancelTimeStamp.
    ?Txn lmdbInsert { ?BookingId cancelled ?CancelTimeStamp. }.
    ?Txn lmdbEnd [].
    
    system log [DDDDDDDDDDDd 3].
    ?CancelTimeStamp [timeStampToString "%d %b %Y %H:%M"] ?CancelTimeStampStr.
    
    system log [DDDDDDDDDDDd 4].
    ["<h1>Booking Cancelled Successfully</h1>
    <div style='background: #d4edda; border: 1px solid #c3e6cb; padding: 1rem; border-radius: 0.375rem; margin: 1rem 0;'>
        <p><strong>Your booking has been cancelled.</strong></p>
    </div>
    
    <h2>Booking Details</h2>
    <p><strong>Name:</strong> " ?BookingName "</p>
    <p><strong>Email:</strong> " ?BookingEmail "</p>
    <p><strong>Phone:</strong> " ?BookingPhone "</p>
    <p><strong>Cancelled At:</strong> " ?CancelTimeStampStr "</p>
    
    <a href='" ?RefererURL "'><button>Go Back</button></a>"] sconcat ?CancelContent.
    
    [?Nav ?CancelContent] buildHTML ?ResponseHTML.
) => (
    ?R response (
        ?RID status 200
        ; headers ( 
            "Content-Type" = "text/html; charset=utf-8". 
        ); body ?ResponseHTML. 
    ).
).

// Booking creation feature
(
    ?R match ( 
        ?RID a get
        ; path ?Path.
    ); navbar ?Nav.
    ?Path [splitString "/"] ["" "event" ?EventId ?Iteration "book"].
    ?Txn lmdbStartR "./db"
    ; lmdbQuery { 
        ?CalendarId event ?EventId. 
        ?EventId name ?EventName
        ; description ?Description
        ; start ?StartT
        ; duration ?DurationT
        ; capacity ?Capacity. 
    }; lmdbEnd [].
    
    ["<h1>Book Event: " ?EventName "</h1>
    <p>" ?Description "</p>
    <form action='/event/" ?EventId "/" ?Iteration "/book' method='post'>
        <label for='name'>Full Name</label>
        <input type='text' id='name' name='name' required>
        
        <label for='email'>Email Address</label>
        <input type='email' id='email' name='email' required>
        
        <label for='phone'>Phone Number</label>
        <input type='tel' id='phone' name='phone' required>
        
        <input type='submit' value='Book Event'>
    </form>"] sconcat ?BookingForm.
    
    [?Nav ?BookingForm] buildHTML ?ResponseHTML.
) => (
    ?R response (
        ?RID status 200
        ; headers ( 
            "Content-Type" = "text/html; charset=utf-8". 
        ); body ?ResponseHTML. 
    ).
).

(
    ?R match ( 
        ?RID a post
        ; path ?Path
        ; body ( 
            name = ?Name. 
            email = ?Email. 
            phone = ?Phone. 
        )
        ; headers ( 
            referer = ?referer.
        ).
    ).
    ?Path [splitString "/"] ["" "event" ?EventId ?IterationS "book"].
    ?IterationS parseInteger ?Iteration.
    ?RID sha256 ?RIDHash.
    ["booking_" ?RIDHash] sconcat ?BookingId.
    system [now >> truncate] ?TimeStamp.

    ?txn lmdbStartRW "./db"
    ; lmdbQuery { ?CalendarId event ?EventId. }
    ; lmdbInsert { 
        ?BookingId a Booking
        ; event ?EventId
        ; iteration ?Iteration
        ; email ?Email
        ; name ?Name
        ; phone ?Phone
        ; timeStamp ?TimeStamp. 
    }
    ; lmdbEnd [].
    
    ["/calendar/" ?CalendarId] sconcat ?RedirectPath.
) => ( ?R response (?RID status 302; headers ( "Location" = ?RedirectPath. ). ).  ).

system runWebServer 3000.
system query [ ?XS ( 
     [[?class -- ?s ?p ?o] (?class = Booking. ?txn lmdbStartR "./db"; lmdbQuery { ?s a ?class; ?p ?o. }; lmdbEnd [].  )] collect ?XS.
)].
// system query [ [subdomain ?s ?Subdomain ?p ?o] ( 
//      ?txn lmdbStartR "./db"; lmdbQuery { ?s tenantSubdomain ?Subdomain; ?p ?o. }; lmdbEnd []. 
// )].
// system query [ [subdomain2 ?s ?Subdomain ] ( 
//      ?txn lmdbStartR "./db"; lmdbQuery { ?s tenantSubdomain ?Subdomain. }; lmdbEnd []. 
// )].

// system query [ deleteEvents (

//     ?Txn lmdbStartRW "./db";
//         lmdbDelete { ?CalendarId event ?EventId. ?EventId ?P ?O. };
//         lmdbEnd [].
//     system cut [].    
// )].